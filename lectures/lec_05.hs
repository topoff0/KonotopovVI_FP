---------------------------------------------------------------------
-- Лекция 05 Ленивые вычисления. Форсирование. Классы типов. Cabal --
---------------------------------------------------------------------

module Lec05 where

import Data.List
import Data.Ord

{-
Ленивые вычисления (ленивая семантика)
В Haskell нет строго последовательность записи функций, они выполняются только при необходимости
Т.е. только при необходимости использовать результат работы функции для получения результата в другой функции

Используя отложенные вычисления можно пользоваться расходимостями, бесконечностями и неопределенностями
Например:
-}

-- Расходящееся вычисление:
myDivergentCalc n = 1 + myDivergentCalc n

-- Неопределенность (расходимость):
myUndefined = undefined

-- Бесконечность:
myInf = [1..]

-- Ошибки:
myError = error "ERROR"

{-
Все эти вычисления валидны, но приводят к проблемам при прямом вызове
НО их все равно можно использовать для продуктивных вычислений или 



Сопоставление с образцом - энергичная операция,
                           происходит сверху вниз, слева направо

Механизм сопоставления с образцом и ленивые вычисления могут элиминировать вычисления
-}

--bar :: ...
bar 1 2 = 3
bar 0 _ = 5
{-
Какие результаты получатся при следующем наборе аргументов:
bar 0 7         -- успех
bar 2 1         -- расходимость (когда она наступила?)
bar 1 (5-3)     -- вычисления по необходимости
bar 1 undefined -- расходимость (когда она наступила?)
bar 0 undefined -- элиминация расходимости



Haskell по умолчанию использует подход вызов-по-необходимости
т.е. по умолчанию нет форсирования вычислений даже при вызове функций
-}

-- Функции могут быть не строгими по аргументу: 
f x = 1
-- f undefined

g x = undefined
-- g 1
-- g
-- :t g


-- Функции могут быть не строгими по аргументу: 
h x = x
-- h undefined

k a = if a > 0 then 1 else undefined
-- k 3
-- k (-1)


{-
thunk - любое еще не вычисленное выражение
        хранится в памяти как нужное, но пока не вычисленное выражение;
        при первом обращении, thunk вычисляется и хранятся как результат и подменяет выражение по необходимости

let x = 1 + 2 in x + x + x
    🠕            🠕   🠕   🠕
    🠕 здесь ничего не вычисляется, просто формируется thunk
                 🠕   🠕   🠕
                 🠕 здесь вызывается thunk, выражение ВЫЧИСЛЯЕТСЯ и заменяется на результат
                     🠕   🠕
                     🠕 здесь ничего не вычисляется, просто подставляется результат
                         🠕
                         🠕 здесь ничего не вычисляется, просто подставляется результат



Управление последовательностью вычислений реализуется через использование отложенных и форсированных вычислений
- если все всегда откладывать, то вычисления со строгой последовательностью действий очень сложно реализовать (например чтение файлов)
- если все всегда форсировать, то вычисления с бесконечностями и расходимостями не получится использовать (например генераторы)



Форсирование вычислений в Haskell реализуется функцией seq
Как функция работает с разными аргументами:
seq 3 4
seq undefined 4
seq (k (-1)) 3
seq (undefined,1) 4
seq (\x -> undefined) 4

Почему результаты такие?
:t seq -- форсирование первого аргумента
Форсирование до конструктора, то есть вычисления все равно не уходит до самого конца (финальный результат не получен)

Зачем вообще такое вычисление? В чем оно помогает?
:t ($!) -- форсирование аппликации (вызов по значению)
f $! x = x `seq` f x

Как будут работать простейшие функции не строгие по аргументу с форсированием
f undefined
f $ undefined
f $! undefined



Пример использования seq в рекурсивных функциях:
-}

factorial n = helper 1 n 
    where helper acc k | k > 1     = helper (acc * k) (k - 1)
                       | otherwise = acc
-- ( ... ((1 * n) * (n - 1)) * (n - 2) * ... * 2) -- thunk для финального acc

factorial' n = helper 1 n 
    where helper acc k | k > 1     = (helper $! acc * k) (k - 1) -- форсирование вычисления аргументов acc
                       | otherwise = acc



-- Использование продуктивных расходимостей:
fibonacci :: [Integer]
fibonacci = 0 : 1 : zipWith (+) fibonacci (tail fibonacci) -- !warning!
-- fibonacci
-- take 15 fibonacci
-- drop 15 fibonacci



{-
Функция iterate генерирует значения, применяя к значению функцию для получения второго значения,
затем применяя ту же самую функцию к этому второму значению для получения третьего значения, и т. д.

iterate f x = [ x, f x, f (f x), f (f (f x)), ... ]
-}

fibonacci2 :: [Integer]
fibonacci2 = map fst $ iterate (\(n, n1) -> (n1, n + n1)) (0,1)
-- fibonacci2 !! 20


{-
Генераторы списков (list comprehension)
Описывают правила построения списков
-}
myListComp = [1..10] -- числа от 1 до 10 с шагом 1
myListCompStep = [1,4..50] -- числа от 1 до 50 с шагом 3
myListCompLet = ['A'..'z']

{-
Как генерировать списки с использованием правил:

Перебираем список через синтаксис генератора:

  структура элементов финального списка
  🠗 разделитель результата и исходных данных и правил
  🠗 🠗
[ x | x <- [1..10] ]
      🠕       🠕
      🠕       список
      элемент обрабатываемого списка

    можно добавить обработку элементов списка
    🠗
[ x * 2 | x <- myListComp ]
               🠕
               используются переменные

     структура может быть любой сложности
     🠗
[ [(x,y)] | x <- "ASD", y <- "zx"]
            🠕           🠕
            в качестве исходных данных используются несколько списков

[ (x,y) | x <- [1..3], y <- [1..x] ]
          🠕                     🠕
          могут быть заданы внутренние зависимости

[ (a,b,c) | a<-myListComp, b<-[1..a], c<-myListComp, a^2 + b^2 == c^2 ]
                                                                🠕
                                                                условия создания элементов списка

Генераторы можно задать любой генератор как map и filter:
[ x^2 | x <- myListComp, even x ]
map (^2) (filter even myListComp)
-}
-- NOTE: Используется для легких условий 



--------------------------------------------------------
{-
Класс типов - это именованный набор имён функций с сигнатурами, параметризованными общим типовым параметром

Как задать класс типов:

  🠗 ------- ключевые слова ------- 🠗
class TypeClassName typeParameter where
          🠕             🠕 переменная типа (типовой параметр)
          🠕 имя класса типа
  functionSignatureOne :: typeParameter -> SomeExistingTypeOne
      🠕
      сигнатура функции задаваемого класса типов
      🠗
  functionSignatureTwo :: typeParameter -> typeParameter -> SomeExistingTypeTwo
  ...

Определение класса типов задает интерфейсы
Для того чтобы пользоваться классами типов нужны представители (экземпляры)



Как написать представителя класса типов:

Задаем тип свой тип:
data MyTypeName = MyDataConstructor

  🠗 ------- ключчевые слова ------- 🠗
instance TypeClassName MyTypeName where
              🠕             🠕 тип представителя которого пишем
              🠕 имя класса типа
  functionSignatureOne MyDataConstructor = FunctionBodyOne
      🠕
      содержательная реализация функций
      🠗
  functionSignatureTwo MyDataConstructor MyDataConstructor = FunctionBodyTwo


functionSignatureOne, functionSignatureTwo - имена функций класса типов TypeClassName





Пример из стандартной библиотеки:
:i Eq -- запрос информации о классе типов:
class Eq a where
  (==) :: a -> a -> Bool
  (/=) :: a -> a -> Bool
  {-# MINIMAL (==) | (/=) #-}

Нет необходимости писать реализации всех интерфейсов для класса типов
Можно реализовать только то, что перечислено в MINIMAL
  в данном случае либо (==) либо (/=)

:t (==)
Имя класса типов задает ограничение, называемое контекстом:
(==) :: Eq a => a -> a -> Bool



В качестве примера возьмем Bool:

data Bool = True | False

instance Eq Bool where -- представитель Eq для типа данных Bool
    True == True = True
    False == False = True
    _ == _ = False
    x /= y = not (x == y)



Реализуем тип игральной кости и представителей основных классов типов для нее:
(будем делать все вручную)
-}

data SixSidedDie' = S1' | S2' | S3' | S4' | S5' | S6'

{-
Класс типов Show
Преобразует значение в строку и может вывести ее на экран
:i Show
...
instance Show SixSidedDie' -- Defined at lec_05.hs:290:10
...
Представитель Show:
-}
instance Show SixSidedDie' where
    show S1' = "One"
    show S2' = "Two"
    show S3' = "Three"
    show S4' = "Four"
    show S5' = "Five"
    show S6' = "Six"

myDie = S6'
-- myDie

{-
Класс типов Read
Обратное преобразование строк в значения
-}
instance Read SixSidedDie' where
    readsPrec _ str = case str of
        'r':'o':'l':'l':' ':'o':'n':'e':rest          -> [(S1',  rest)]
        'r':'o':'l':'l':' ':'t':'w':'o':rest          -> [(S2',  rest)]
        'r':'o':'l':'l':' ':'t':'h':'r':'e':'e':rest  -> [(S3',  rest)]
        'r':'o':'l':'l':' ':'f':'o':'u':'r':rest      -> [(S4',  rest)]
        'r':'o':'l':'l':' ':'f':'i':'v':'e':rest      -> [(S5',  rest)]
        'r':'o':'l':'l':' ':'s':'i':'x':rest          -> [(S6',  rest)]
-- (read "roll two")
-- (read "roll two") :: SixSidedDie'

{-
Представитель Eq:
-}
instance Eq SixSidedDie' where
    (==) S6' S6' = True
    (==) S5' S5' = True
    (==) S4' S4' = True
    (==) S3' S3' = True
    (==) S2' S2' = True
    (==) S1' S1' = True
    (==) _   _   = False
    (/=) x   y   = not (x == y)
-- myDie == S5'
-- myDie == S6'

{-
Класс типов Ord
Упорядочивание значений.
Если для типа имеет смысл понятия: больше, меньше, сортировка, сравнение, min, max, ...
:i Ord

        тип уже должен быть представителем Eq для того что бы сделать его представителем Ord
        (т.е. классы расширяемы (class extantion), а интерфейсы могут наследоваться)
        Ord наследует Eq
        🠗
class Eq a => Ord a where
  compare :: a -> a -> Ordering
  (<) :: a -> a -> Bool
  (<=) :: a -> a -> Bool
  (>) :: a -> a -> Bool
  (>=) :: a -> a -> Bool
  max :: a -> a -> a
  min :: a -> a -> a
  {-# MINIMAL compare | (<=) #-}


Законы для класса типов Ord (контроль выполнения законов на программисте):

1. x <= x ≡ True               -- Reflexivity

2. if x <= y && y <= z ≡ True,
   then x <= z ≡ True          -- Transitivity
   else x <= z ≡ False

3. if x <= y && y <= x ≡ True,
   then x == y ≡ True          -- Antisymmetry
   else x == y ≡ False

4. x <= y || y <= x ≡ True     -- Comparability

-}

instance Ord SixSidedDie' where
    compare S6' S6' = EQ
    compare S6' _   = GT
    compare _   S6' = LT
    compare S5' S5' = EQ
    compare S5' _   = GT
    compare _   S5' = LT
    compare S4' S4' = EQ
    compare S4' _   = GT
    compare _   S4' = LT
    compare S3' S3' = EQ
    compare S3' _   = GT
    compare _   S3' = LT
    compare S2' S2' = EQ
    compare S2' _   = GT
    compare _   S2' = LT
    compare S1' S1' = EQ
    compare S1' _   = GT -- !warning!
    compare _   S1' = LT -- !warning!
    compare _   _   = undefined -- !warning!
-- myDie > S5'
-- myDie <= S5'

{-
Минимальное определение Ord: compare | (<=)

Через (<=) можно вывести compare:
compare x y = if x == y then EQ
                        else if x <= y then LT
                                       else GT

Через compare можно вывести все остальные функции Ord:

x < y = case compare x y of {LT -> True; _ -> False}
x <= y = case compare x y of {GT -> False; _ -> True}
x > y = case compare x y of {GT -> True; _ -> False}
x >= y = case compare x y of {LT -> False; _ -> True}
max x y = if x <= y then y else x
min x y = if x <= y then x else y
-}

{-
Класс типов Enum
Перечислимые типы. Работа с последовательностями значений

class Enum a where
  succ :: a -> a
  pred :: a -> a
  toEnum :: Int -> a
  fromEnum :: a -> Int
  enumFrom :: a -> [a]
  enumFromThen :: a -> a -> [a]
  enumFromTo :: a -> a -> [a]
  enumFromThenTo :: a -> a -> a -> [a]
  {-# MINIMAL toEnum, fromEnum #-}
-}

instance Enum SixSidedDie' where
    toEnum 0 = S1'
    toEnum 1 = S2'
    toEnum 2 = S3'
    toEnum 3 = S4'
    toEnum 4 = S5'
    toEnum 5 = S6'
    fromEnum S1' = 0
    fromEnum S2' = 1
    fromEnum S3' = 2
    fromEnum S4' = 3
    fromEnum S5' = 4
    fromEnum S6' = 5
-- succ S1'
-- pred S5'

{-
Класс типов Bounded
Минимальное и максимальное значение типов

class Bounded a where
  minBound :: a
  maxBound :: a
  {-# MINIMAL minBound, maxBound #-}

-- :i Int
-- :i Integer

-}
instance Bounded SixSidedDie' where
    minBound = S1'
    maxBound = S6'


{-
Для простых типов можно использовать механизм deriving, который попробует вывести типы самостоятельно
Это хорошо работает для простых типов и при прототипировании

Например, все что написано выше реализуется:
-}
data SixSidedDie = S1 | S2 | S3 | S4 | S5 | S6 deriving (Show, Read, Eq, Ord, Enum, Bounded)
myAnotherDie = S3
-- myAnotherDie
-- succ myAnotherDie
-- myAnotherDie > S5
-- sort [myAnotherDie, S5, S1]
-- (read "S1") :: SixSidedDie
{-
Но он работает по стандартной схеме и нужно заранее понимать как будут использоваться конструкторы данных

Например разная последовательность записи конструкторов даст разные результаты:
-}
data Test1 = AA | ZZ deriving (Eq, Ord)
data Test2 = ZZZ | AAA deriving (Eq, Ord)
-- AA < ZZ
-- AAA < ZZZ
-- AA > ZZ
-- AAA > ZZZ

{-
Можно использовать deriving и рукописных представителей одновременно
+ внутри экземпляра учитываются уже написанные представители
-}
newtype MyName = MyName (String, String) deriving (Show, Eq)

instance Ord MyName where
    compare (MyName (f1, l1)) (MyName (f2, l2)) = compare (l1, f1) (l2, f2)

names = [MyName ("Emil", "Cioran"), MyName ("Eugene", "Thacker"), MyName ("Friedrich", "Nietzsche")]
-- sort names

{-
Синтаксис записей также используется в представителях классов типов:
-}
data MyNameRec = MyNameRec
  { firstName :: String
  , lastName  :: String
  } deriving (Show, Eq)

instance Ord MyNameRec where
  compare = comparing lastName

namesRec = [MyNameRec "Emil" "Cioran", MyNameRec "Eugene" "Thacker", MyNameRec "Friedrich" "Nietzsche"]
-- sort namesRec



{-
Другие полезные классы типов:

Класс типов Num
Возможность работы с числовыми типами и обобщенный код для работы с арифметическими операциями

         допустимо множественное наследование
         🠗       🠗
class (Eq a, Show a) => Num a where -- 
    (+), (-), (*) :: a -> a -> a
    negate :: a -> a
    abs, signum :: a -> a
    fromInteger :: Integer -> a
    ...



Ккласс типов Real
Точные вычисления и преобразование чисел в рациональные

class (Num a, Ord a) => Real a where
  toRational :: a -> Rational
  {-# MINIMAL toRational #-}



Класс типов Integral
Работа с целочисленными типами (Int, Integer)

class (Real a, Enum a) => Integral a where
    ...



Класс типов Fractional
Работa с дробными типами (Float, Double)

class Num a => Fractional a where
    ...



Класс типов Floating
Работа с математическими функциями (тригонометрия, логарифмы, ...)

class Fractional a => Floating a where
    ...



Автоматического приведения типов нет, но есть полиморфные функции

-}





--------------------------------------------------------
{-
Cabal (Common Architecture for Building Applications and Libraries) — библиотека и система сборки
- Сборка проекта из нескольких модулей (структурирование проекта)
- Управление зависимостями (подключение внешних библиотек)
- Изоляция от окружения (локально хранит нужные версии библиотек и контролирует их совместимость)
- Автоматически скачивает библиотеки из Hackage (центрального репозитория Haskell)
- Сборка проектов, запуск тестов, ...

-------------------------------
-- Создание проекта в cabal: --
-------------------------------

1. Создание директории:
mkdir myCabalProject
cd myCabalProject

2. Инициализация проекта:
cabal init
-- ответы вопросы по специфике проекта (выбрать значения по умолчанию)
-- Executable - программа с точкой входа main
-- Main.hs - точка входа
-- app - директория для проекта
-- Haskell2010 - актуальная версия языка

Итог после выполнения п.1 и 2:
myCabalProject
  |- app
  |   |- Main.hs -- исходный код
  |- CHANGELOG.md
  |- LICENSE
  |- myCabalProject.cabal -- описание пакетов, зависимостей и прочие метаданные
                             (важный файл, можно все поломать, пишите аккуратно)
--NOTE: Написать автора и Email свой с Github

3. Сборка проекта (выполняется в корневом каталоге проекта)
cabal build
-- cabal скачает необходимые зависимости из Hackage, скомпилирует их и проект (все новое закэшируется в папке)
-- ! появится новая директория dist-newstyle

4. Запуск проекта
cabal run
-- автоматически выполнит build если есть изменения
-- На выходе получим результаты работы main: Hello, Haskell!


~. Запуск интерактивного режима для удобства разработки (Read-Eval-Print Loop)
cabal repl
-- ! загрузится ghci, но в этом случае она будет содержать в себе все функции, библиотеки, модули и пр. которые есть в проекте с учетом доступа и видимости из main

-------------------------------
Добавление зависимостей в проект:

Заходим в файл _.cabal (в нашем случае myCabalProject.cabal)
    Ищем 
          executable _ (в нашем случае executable myCabalProject)
    Ищем поле 
              build-depends:    base ... 
    Записываем под base дополнительные пакеты для скачивания например text (версии можно указывать гибко (>=), фиксировано (==) и в диапазоне (>=1.2 && <1.3))
                                text >=1.2.5
Импортируем библиотеки в модуль (в нашем случае Main)
  import qualified Data.Text as T
Пере собираем проект (cabal build) и cabal автоматически проанализирует и подгрузит новые нужные библиотеки



Добавим в проект модуль MyModule и импортируем его в Main и настроим ограничения доступа к функциям
Импортировать всё и везде не нужно, ограничивайтесь только теми участками кода, в которых используются функции
-- NOTE: Написать в .cabal other-modules при добавлении новых файлов



Если проект уже содержит в себе все зависимости (они кэшированы) можно собирать проект оффлайн:
cabal build --offline
cabal run --offline

Для старых проектов полезно замораживать версии пакетов (т.к. скачивание новых версий может сломать программу):
cabal freeze
-- cabal freeze создаст новый файл (cabal.project.freeze) с точными версиями пакетов и при следующих сборках cabal будет придерживаться точных версий пакетов
-------------------------------

-}
